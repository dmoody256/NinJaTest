import os
import sys
import rtconfig

if os.getenv('RTT_ROOT'):
    RTT_ROOT = os.getenv('RTT_ROOT')
else:
    RTT_ROOT = os.path.normpath(os.getcwd() + '/../..')

sys.path = sys.path + [os.path.join(RTT_ROOT, 'tools')]
from building import *

TARGET = 'rtthread-beaglebone.' + rtconfig.TARGET_EXT

#SetOption('experimental','ninja')
DefaultEnvironment(tools=[])
env = Environment(tools = ['mingw', 'textfile'],
    AS = rtconfig.AS, ASFLAGS = rtconfig.AFLAGS,
    CC = rtconfig.CC, CFLAGS = rtconfig.CFLAGS,
    CXX= rtconfig.CXX, CXXFLAGS = rtconfig.CFLAGS,
    AR = rtconfig.AR, ARFLAGS = '-rc',
    LINK = rtconfig.LINK, LINKFLAGS = rtconfig.LFLAGS)
env.PrependENVPath('PATH', rtconfig.EXEC_PATH)
env.Tool('ninja')

Export('RTT_ROOT')
Export('rtconfig')

def ExpandList(l, expandList = []):
    for x in l:
        if type(x) != list:
            expandList.append(x)
        else:
            ExpandList(x)
    return expandList

# prepare building environment
objs = ExpandList(PrepareBuilding(env, RTT_ROOT))

cppPathStr = "-I" + "\n-I".join(env["CPPPATH"]).replace("\\", "/")
compileIncPath = os.path.normpath(os.path.join(rtconfig.EXEC_PATH, "..", "include"))
incPathsFile = env.Textfile("build/__cpp_path.txt", [
    "-I%s\n" % (os.path.join(compileIncPath, "libcxx").replace("\\", "/")),
    "-I%s\n" % (compileIncPath.replace("\\", "/")),
    cppPathStr
])

defineOptsFile = env.Textfile("build/__define_options.txt", [
    "-DTEST1"
])

objsList = []
for obj in objs:
    (fileName, ext) = os.path.splitext(obj.path)
    if ext in env["CPPSUFFIXES"] or ext == ".o":
        objsList += [fileName.replace("\\", "/") + ".o"];
linkObjsFile = env.Textfile(
    "build/__link_objects.txt",
    "\n".join(objsList)
)

def _add_scanner(builder):
    def new_scanner(node, env, path):
        return incPathsFile + defineOptsFile + linkObjsFile
       
    builder.builder.target_scanner = SCons.Scanner.Scanner(
        function=new_scanner,
        path_function=[],
    )
for object_builder in SCons.Tool.createObjBuilders(env):
    _add_scanner(object_builder)

env["_CPPINCFLAGS"] = "@%s @%s" % (incPathsFile[0].path, defineOptsFile[0].path)
env["LINKCOM"] = "$LINK -o $TARGET $LINKFLAGS $__RPATH $_LIBDIRFLAGS $_LIBFLAGS @%s" % (linkObjsFile[0].path.replace("\\", "/"))

# make a building
DoBuilding(TARGET, objs)
